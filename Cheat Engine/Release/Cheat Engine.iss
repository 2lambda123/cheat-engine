; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=Cheat Engine 5.6
AppVerName=Cheat Engine 5.6
AppPublisher=Dark Byte
AppPublisherURL=http://www.cheatengine.org/
AppSupportURL=http://www.cheatengine.org/
AppUpdatesURL=http://www.cheatengine.org/
DefaultDirName={pf}\Cheat Engine
DefaultGroupName=Cheat Engine 5.6
AllowNoIcons=yes
LicenseFile=..\Release\License.txt
InfoAfterFile=..\Release\readme.txt
OutputBaseFilename=CheatEngine56
PrivilegesRequired=admin
ChangesAssociations=yes

[UninstallDelete]
Type: files; Name: "{app}\kerneldata.dat"
Type: files; Name: "{app}\CEProtect.dat"
Type: files; Name: "{app}\ADDRESSESFIRST.TMP"
Type: files; Name: "{app}\MEMORYFIRST.TMP"
Type: files; Name: "{app}\ADDRESSES.TMP"
Type: files; Name: "{app}\MEMORY.TMP"

[InstallDelete]
Type: files; Name: "{app}\kerneldata.dat"
Type: files; Name: "{app}\CEProtect.dat"
Type: files; Name: "{app}\ADDRESSESFIRST.TMP"
Type: files; Name: "{app}\MEMORYFIRST.TMP"
Type: files; Name: "{app}\ADDRESSES.TMP"
Type: files; Name: "{app}\MEMORY.TMP"

[Registry]
Root: HKCR; Subkey: ".CT"; ValueType: string; ValueName: ""; ValueData: "CheatEngine"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "CheatEngine"; ValueType: string; ValueName: ""; ValueData: "Cheat Engine"; Flags: uninsdeletekey
Root: HKCR; Subkey: "CheatEngine\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\Cheat Engine.exe,0"
Root: HKCR; Subkey: "CheatEngine\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Cheat Engine.exe"" ""%1"""

[Tasks]
Name: "desktopicon"; Description: "Create a &desktop icon"; GroupDescription: "Additional icons:"

[code]
procedure UninstallDriver();
var
  ResultCode: Integer;
begin
  Exec(ExpandConstant('{app}Kernelmoduleunloader.exe'),'/SETUP',ExpandConstant('{app}'),0,ewWaitUntilTerminated,ResultCode);
  //try to delete the dbk32.sys file if there is one there
  
  DeleteFile(ExpandConstant('{app}dbk32.sys'));
  DeleteFile(ExpandConstant('{app}dbk32.bak'));

  RenameFile(ExpandConstant('{app}dbk32.sys'),ExpandConstant('{app}dbk32.bak'));
  
  //lets remove all settings as well
  
  
end;

function NeedRestart(): Boolean;
var restart: boolean;
    ResultDWord: Cardinal;
begin
  restart:=false;

  if RegQueryDWordValue(HKCU,'Software\Cheat Engine','Use Processwatcher',ResultDWord) then
    if ResultDWord<>0 then restart:=true;

  if RegQueryDWordValue(HKCU,'Software\Cheat Engine','Use Kernel Debugger',ResultDWord) then
    if ResultDWord<>0 then restart:=true;
    
  result:=restart;
end;

//post install
procedure CurStepChanged(CurStep: TSetupStep);
var ResultDWord: Cardinal;
begin
  if CurStep=ssPostInstall then
  begin
    if RegKeyExists(HKEY_CURRENT_USER, 'Software\Cheat Engine') then
    begin
      if not RegQueryDWordValue(HKCU,'Software\Cheat Engine','First Time User',ResultDWord) then
        ResultDWord:=1;
    
      RegDeleteKeyIncludingSubkeys(HKEY_CURRENT_USER,'Software\Cheat Engine');
      RegWriteDWordValue(HKEY_CURRENT_USER,  'Software\Cheat Engine', 'First Time User',ResultDWord);
    end;
  end;
end;

//uninstall

function InitializeUninstall(): Boolean;
begin
  //run the unloader
  UninstallDriver();
  result:=true;
end;

function UninstallNeedRestart(): Boolean;
var ResultDWord: Cardinal;
begin
  result:=false;
  //check the registry
  //if the kernel debugger is enabled, or the processwatcher enabled then return true
  UninstallDriver(); //second time removes the link if the first time failed
  
  if RegQueryDWordValue(HKCU,'Software\Cheat Engine','Use Processwatcher',ResultDWord) then
    if ResultDWord<>0 then result:=true;
    
  if RegQueryDWordValue(HKCU,'Software\Cheat Engine','Use Kernel Debugger',ResultDWord) then
    if ResultDWord<>0 then result:=true;


  //delete the reg
  RegDeleteKeyIncludingSubkeys(HKEY_CURRENT_USER,'Software\Cheat Engine');

end;

[Dirs]
;go ahead, scream like a pig about this one, I know you want to
Name: "{app}"; permissions: everyone-full;


[Files]
Source: "..\bin\cheatengine.exe"; DestDir: "{app}"; DestName: "Cheat Engine.exe"; Flags: ignoreversion
Source: "..\bin\vmdisk.img"; DestDir: "{app}"; DestName: "vmdisk.img"; Flags: ignoreversion
Source: "..\bin\CEHook.dll"; DestDir: "{app}"; DestName: "CEHook.dll"; Flags: ignoreversion
Source: "..\bin\speedhack.dll"; DestDir: "{app}"; DestName: "speedhack.dll"; Flags: ignoreversion

Source: "..\bin\commonmodulelist.txt"; DestDir: "{app}"; DestName: "commonmodulelist.txt"; Flags: ignoreversion
Source: "..\LockedString.bmp"; DestDir: "{app}"; DestName: "LockedString.bmp"; Flags: ignoreversion
Source: "..\UnLockedString.bmp"; DestDir: "{app}"; DestName: "UnLockedString.bmp"; Flags: ignoreversion
Source: "..\TextureString.bmp"; DestDir: "{app}"; DestName: "TextureString.bmp"; Flags: ignoreversion
Source: "..\targettexture.bmp"; DestDir: "{app}"; DestName: "targettexture.bmp"; Flags: ignoreversion
Source: "..\movementtexture.bmp"; DestDir: "{app}"; DestName: "movementtexture.bmp"; Flags: ignoreversion
Source: "..\Locktexture.bmp"; DestDir: "{app}"; DestName: "Locktexture.bmp"; Flags: ignoreversion
Source: "..\Black.bmp"; DestDir: "{app}"; DestName: "Black.bmp"; Flags: ignoreversion
Source: "..\bin\dxhook.dll"; DestDir: "{app}"; DestName: "dxhook.dll"; Flags: ignoreversion
Source: "..\directxhook\D3DX81ab.dll"; DestDir: "{sys}"; DestName: "D3DX81ab.dll";
Source: "..\directxhook\d3dx9.dll"; DestDir: "{sys}"; DestName: "d3dx9.dll";

Source: "..\bin\ceregreset.exe"; DestDir: "{app}"; DestName: "ceregreset.exe"; Flags: ignoreversion
Source: "..\bin\Kernelmoduleunloader.exe"; DestDir: "{app}"; DestName: "Kernelmoduleunloader.exe"; Flags: ignoreversion
;Source: "..\driver.dat"; DestDir: "{app}"; DestName: "driver.dat"; Flags: ignoreversion
;comment the next 3 lines for the baby version
Source: "..\bin\dbk32.dll"; DestDir: "{app}"; DestName: "dbk32.dll"; Flags: ignoreversion
Source: "..\bin\dbk32.sys"; DestDir: "{app}"; DestName: "dbk32.sys"; Flags: ignoreversion; BeforeInstall: UninstallDriver
Source: "..\bin\dbk64.sys"; DestDir: "{app}"; DestName: "dbk64.sys"; Flags: ignoreversion;
Source: "..\bin\EmptyProcess.exe"; DestDir: "{app}"; DestName: "EmptyProcess.exe"; Flags: ignoreversion
Source: "..\bin\EmptyDLL.dll"; DestDir: "{app}"; DestName: "EmptyDLL.dll"; Flags: ignoreversion
Source: "..\bin\Systemcallretriever.exe"; DestDir: "{app}"; DestName: "Systemcallretriever.exe"; Flags: ignoreversion
Source: "..\bin\systemcallsignal.exe"; DestDir: "{app}"; DestName: "systemcallsignal.exe"; Flags: ignoreversion

;Source: "..\stealth.dll"; DestDir: "{app}"; DestName: "stealth.dll"; Flags: ignoreversion

Source: "..\bin\CheatEngine.chm"; DestDir: "{app}"; DestName: "CheatEngine.chm"; Flags: ignoreversion

Source: "..\bin\tutorial.exe"; DestDir: "{app}"; DestName: "Tutorial.exe"; Flags: ignoreversion
Source: "..\bin\allochook.dll"; DestDir: "{app}"; DestName: "allochook.dll"; Flags: ignoreversion

;Source: "..\Cheat Engine Net\Client\client.exe"; DestDir: "{app}"; DestName: "Cheat Engine Client.exe"; Flags: ignoreversion
;Source: "..\Cheat Engine Net\Server\ceserver.exe"; DestDir: "{app}"; DestName: "Cheat Engine Server.exe"; Flags: ignoreversion
;Source: "..\Cheat Engine Client.exe"; DestDir: "{app}"; DestName: "Cheat Engine Client.exe"; Flags: ignoreversion
;Source: "..\Cheat Engine Server.exe"; DestDir: "{app}"; DestName: "Cheat Engine Server.exe"; Flags: ignoreversion


;plugin sdk
Source: "..\plugin\cepluginsdk.pas"; DestDir: "{app}\Plugins\"; Flags: ignoreversion
Source: "..\plugin\cepluginsdk.h"; DestDir: "{app}\Plugins\"; Flags: ignoreversion

; Plugin example
Source: "..\plugin\example-c\release\example-c.dll"; DestDir: "{app}\Plugins\example-c\"; DestName: "example-c.dll"; Flags: ignoreversion
Source: "..\plugin\example-c\example-c.c"; DestDir: "{app}\Plugins\example-c\"; DestName: "example-c.c"; Flags: ignoreversion
Source: "..\plugin\example-c\example-c.def"; DestDir: "{app}\Plugins\example-c\"; DestName: "example-c.def"; Flags: ignoreversion
;Source: "..\plugin\example-c\ceplugin.h"; DestDir: "{app}\Plugins\example-c\"; DestName: "plugin.h"; Flags: ignoreversion
Source: "..\plugin\example-c\example-c.sln"; DestDir: "{app}\Plugins\example-c\"; DestName: "example-c.sln"; Flags: ignoreversion
Source: "..\plugin\example-c\example-c.vcproj"; DestDir: "{app}\Plugins\example-c\"; DestName: "example-c.vcproj"; Flags: ignoreversion

; Delphi Plugin example
Source: "..\plugin\example\exampleplugin.dll"; DestDir: "{app}\Plugins\example-delphi\"; DestName: "exampleplugin.dll"; Flags: ignoreversion
Source: "..\plugin\example\exampleplugin.cfg"; DestDir: "{app}\Plugins\example-delphi\"; DestName: "exampleplugin.cfg"; Flags: ignoreversion
Source: "..\plugin\example\exampleplugin.dof"; DestDir: "{app}\Plugins\example-delphi\"; DestName: "exampleplugin.dof"; Flags: ignoreversion
Source: "..\plugin\example\exampleplugin.res"; DestDir: "{app}\Plugins\example-delphi\"; DestName: "exampleplugin.res"; Flags: ignoreversion
Source: "..\plugin\example\exampleplugin.dpr"; DestDir: "{app}\Plugins\example-delphi\"; DestName: "exampleplugin.dpr"; Flags: ignoreversion
Source: "..\plugin\example\Unit1.pas"; DestDir: "{app}\Plugins\example-delphi\"; DestName: "Unit1.pas"; Flags: ignoreversion

;Packet editor plugin example
;Source: "..\plugin\packet editor\packeteditor.dll"; DestDir: "{app}\Plugins\example packet editor\"; Flags: ignoreversion
;Source: "..\plugin\packet editor\inject\cepe.dll"; DestDir: "{app}\Plugins\example packet editor\inject\"; Flags: ignoreversion

;Packet editor plugin example source
Source: "..\plugin\packet editor\src\packeteditor.cfg"; DestDir: "{app}\Plugins\example packet editor\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\src\packeteditor.dof"; DestDir: "{app}\Plugins\example packet editor\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\src\packeteditor.dpr"; DestDir: "{app}\Plugins\example packet editor\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\src\packeteditor.res"; DestDir: "{app}\Plugins\example packet editor\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\src\Unit1.pas"; DestDir: "{app}\Plugins\example packet editor\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\src\injector.pas"; DestDir: "{app}\Plugins\example packet editor\src\"; Flags: ignoreversion
;Source: "..\plugin\packet editor\src\cepluginsdk.pas"; DestDir: "{app}\Plugins\example packet editor\src\"; Flags: ignoreversion

Source: "..\plugin\packet editor\inject\src\cepe.cfg"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\inject\src\cepe.dof"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\inject\src\cepe.dpr"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\inject\src\cepe.res"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\inject\src\filterform.dfm"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\inject\src\filterform.pas"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\inject\src\hexedit.pas"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\inject\src\mainunit.dfm"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\inject\src\mainunit.pas"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion
Source: "..\plugin\packet editor\inject\src\packetfilter.pas"; DestDir: "{app}\Plugins\example packet editor\inject\src\"; Flags: ignoreversion

;Debug event log example

Source: "..\plugin\DebugEventLog\src\DebugEventLog.dll"; DestDir: "{app}\Plugins\DebugEventLog\"; Flags: ignoreversion

;Debug event log example source
Source: "..\plugin\DebugEventLog\src\DebugEventLog.cfg"; DestDir: "{app}\Plugins\DebugEventLog\src"; Flags: ignoreversion
Source: "..\plugin\DebugEventLog\src\DebugEventLog.dpr"; DestDir: "{app}\Plugins\DebugEventLog\src"; Flags: ignoreversion
Source: "..\plugin\DebugEventLog\src\DebugEventLog.res"; DestDir: "{app}\Plugins\DebugEventLog\src"; Flags: ignoreversion
Source: "..\plugin\DebugEventLog\src\exportimplementation.pas"; DestDir: "{app}\Plugins\DebugEventLog\src"; Flags: ignoreversion
Source: "..\plugin\DebugEventLog\src\frmEventLogUnit.dfm"; DestDir: "{app}\Plugins\DebugEventLog\src"; Flags: ignoreversion
Source: "..\plugin\DebugEventLog\src\frmEventLogUnit.pas"; DestDir: "{app}\Plugins\DebugEventLog\src"; Flags: ignoreversion



Source: "..\include\*"; DestDir: "{app}\include\";
Source: "..\example scripts\*"; DestDir: "{app}\example scripts\";
Source: "..\bin\ucc12.dll"; DestDir: "{app}";
Source: "..\bin\undercdll.dll"; DestDir: "{app}";

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\Cheat Engine 5.6"; Filename: "{app}\Cheat Engine.exe"
Name: "{group}\Cheat Engine tutorial"; Filename: "{app}\Tutorial.exe"
Name: "{group}\Cheat Engine help"; Filename: "{app}\CheatEngine.chm"

Name: "{group}\Kernel stuff\Unload kernel module"; Filename: "{app}\Kernelmoduleunloader.exe"
Name: "{group}\Kernel stuff\Gather kernel data"; Filename: "{app}\Systemcallretriever.exe"
Name: "{group}\Reset settings"; Filename: "{app}\ceregreset.exe"

Name: "{group}\Uninstall Cheat Engine"; Filename: "{uninstallexe}"
Name: "{userdesktop}\Cheat Engine"; Filename: "{app}\Cheat Engine.exe"; Tasks: desktopicon

[Run]
Filename: "{app}\Cheat Engine.exe"; Description: "Launch Cheat Engine 5.6"; Flags: nowait postinstall skipifsilent runascurrentuser
